
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

class NullObject public
{
}

func parse(buffer as buffer) static as object
{
	if buffer == null:
		return null
	return forBuffer(buffer).acceptObject()
}

func parse(str as string) static as object
{
	if String.isEmpty(str):
		return null
	return forString(str).acceptObject()
}

func parse(file as File) static as object
{
	if file == null:
		return null
	return parse(file.getContentsString("UTF-8"))
}

func forString(str as string) static as this
{
	var v = new this()
	v.setDataString(str)
	return v
}

func forBuffer(buffer as buffer) static as this
{
	var v = new this()
	v.setDataBuffer(buffer)
	return v
}

var iterator as CharacterIterator

func setDataString(str as string)
{
	iterator = CharacterIteratorForString.forString(str)
	iterator.moveToNextCharacter()
}

func setDataBuffer(buffer as buffer)
{
	iterator = CharacterIteratorForBuffer.forBuffer(buffer)
	iterator.moveToNextCharacter()
}

func skipSpaces private
{
	loop {
		if iterator.hasEnded():
			break
		var c = iterator.getCurrentCharacter()
		if c == ' ' || c == '\t' || c == '\r' || c == '\n' {
			iterator.moveToNextCharacter()
			continue
		}
		break
	}
}

func acceptCharacter(c as char) private as bool
{
	skipSpaces()
	if iterator.getCurrentCharacter() == c {
		iterator.moveToNextCharacter()
		return true
	}
	return false
}

func acceptString private as string
{
	skipSpaces()
	var ss = iterator.getCurrentCharacter()
	if ss != '\'' && ss != '"':
		return null
	var i = 0
	var sb = new StringBuilder()
	loop {
		var c = iterator.getNextCharacter()
		i ++
		if Character.isEOF(c as char) {
			for(var n = 0 ; n < i ; n++):
				iterator.moveToPreviousCharacter()
			return null
		}
		if c == ss {
			iterator.moveToNextCharacter()
			break
		}
		if c == '\\' {
			c = iterator.getNextCharacter()
			if c == 'u' {
				var v = new StringBuilder()
				var x = 0
				while x < 4 {
					if iterator.hasEnded():
						break
					v.appendCharacter(iterator.getNextCharacter())
					x++
				}
				c = String.toIntegerFromHex(v.toString()) as long
			}
			else if c == 't' {
				c = '\t' as long
			}
			else if c == 'r' {
				c = '\r' as long
			}
			else if c == 'n' {
				c = '\n' as long
			}
			else if c == 'b' {
				c = '\b' as long
			}
			else if c == 'f' {
				c = '\f' as long
			}
		}
		sb.appendCharacter(c)
	}
	return sb.toString()
}

func acceptBoolean private as BooleanObject
{
	skipSpaces()
	var ss = iterator.getCurrentCharacter()
	if ss != 't' && ss != 'f':
		return null
	var sb = new StringBuilder()
	sb.appendCharacter(ss)
	var li = 5
	if ss == 't':
		li = 4
	var btc = 0
	loop {
		var c = iterator.getNextCharacter()
		btc++
		if c != 'a' && c != 'l' && c != 's' && c != 'e' && c != 'r' && c != 'u' {
			iterator.moveToNextCharacter()
			btc++
			break
		}
		sb.appendCharacter(c)
		if sb.count() == li {
			iterator.moveToNextCharacter()
			btc++
			break
		}
	}
	var v = sb.toString()
	if li == 4 && "true" == v:
		return Boolean.asObject(true)
	if li == 5 && "false" == v:
		return Boolean.asObject(false)
	var i = 0
	while i < btc {
		iterator.moveToPreviousCharacter()
		i++
	}
	return null
}

func acceptNumber private as object
{
	skipSpaces()
	var ss = iterator.getCurrentCharacter()
	if ss != '-' && ss != '+' && ss != '.' && (ss < '0' || ss > '9'):
		return null
	var sb = new StringBuilder()
	sb.appendCharacter(ss)
	loop {
		var c = iterator.getNextCharacter()
		if c != '.' && c != 'e' && c != 'E' && c != '-' && (c < '0' || c > '9'):
			break
		sb.appendCharacter(c)
	}
	var s = sb.toString()
	if String.getIndexOf(s, '.') > -1:
		return Double.asObject(Double.asDouble(s))
	return Integer.asObject(Integer.asInteger(s))
}

func acceptNull private as NullObject
{
	skipSpaces()
	var ss = iterator.getCurrentCharacter()
	if ss != 'n':
		return null
	var sb = new StringBuilder()
	sb.appendCharacter(ss)
	var btc = 0
	loop {
		var c = iterator.getNextCharacter()
		btc++
		if c != 'u' && c != 'l' {
			iterator.moveToNextCharacter()
			btc++
			break
		}
		sb.appendCharacter(c)
		if sb.count() == 4 {
			iterator.moveToNextCharacter()
			btc++
			break
		}
	}
	if "null" == sb.toString():
		return new NullObject()
	var i = 0
	while i < btc {
		iterator.moveToPreviousCharacter()
		i++
	}
	return null
}

func acceptObject private as object
{
	if acceptCharacter('[') {
		var v = new DynamicVector()
		loop {
			if acceptCharacter(']'):
				break
			var o = acceptObject()
			if o == null:
				return null
			v.append(o)
			acceptCharacter(',')
		}
		return v
	}
	if acceptCharacter('{') {
		var v = new DynamicMap()
		loop {
			if acceptCharacter('}'):
				break
			var key = acceptString()
			if key == null:
				return null
			if acceptCharacter(':') == false:
				return null
			var val = acceptObject()
			if val == null:
				return null
			if val is NullObject {
				v.set(key, null)
			}
			else {
				v.set(key, val)
			}
			acceptCharacter(',')
		}
		return v
	}
	var s = acceptString()
	if s:
		return s
	var b = acceptBoolean()
	if b:
		return b
	var n = acceptNull()
	if n:
		return n
	var v = acceptNumber()
	if v:
		return v
	return null
}

func getNextToken as object
{
	if acceptCharacter('['):
		return "["
	if acceptCharacter(']'):
		return "]"
	if acceptCharacter('{'):
		return "{"
	if acceptCharacter('}'):
		return "}"
	if acceptCharacter(','):
		return ","
	var s = acceptString()
	if s:
		return s
	var b = acceptBoolean()
	if b:
		return b
	var n = acceptNull()
	if n:
		return n
	var v = acceptNumber()
	if v:
		return v
	return null
}

func pushData(string as string)
{
	assert string
	if iterator == null {
		setDataString(string)
		return
	}
	var sb = new StringBuilder()
	loop {
		var c = iterator.getNextCharacter()
		if Character.isEOF(c as char):
			break
		sb.appendCharacter(c)
	}
	sb.append(string)
	setDataString(sb.toString())
}
